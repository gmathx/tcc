node shift(time:int) returns (d,m,a,n:bool)
let
	automaton
		state Dawn do
			d = true; m = false; a = false; n = false;
		unless time >= 8 & time < 12 then Morning
			 | time >= 12 & time < 17 then Afternoon
			 | time >= 17 then Night

		state Morning do
			d = false; m = true; a = false; n = false;
		unless time < 8 then Dawn
			 | time >= 12 & time < 17 then Afternoon
			 | time >= 17 then Night
		
		state Afternoon do
			d = false; m = false; a = true; n = false;
		unless time < 8 then Dawn
			 | time >= 8 & time < 12 then Morning
			 | time >= 17 then Night

		
		state Night do
			d = false; m = false; a = false; n = true;
		unless time < 8 then Dawn
			 | time >= 8 & time < 12 then Morning
			 | time >= 12 & time < 17 then Afternoon
	end
tel

node heating(c:bool) returns (status:bool)
let
	automaton
		state Off do
			status = false;
		unless c then On

		state On do
			status = true;
		unless not c then Off
	end
tel

node cooling(c:bool) returns (status:bool)
let
	automaton
		state Off do
			status = false;
		unless c then On

		state On do
			status = true;
		unless not c then Off
	end
tel

node greenhouse(t_in,time:int) returns (heating_on,cooling_on:bool)
contract
	var too_hot,too_cold,both_on,d,m,a,n,t1,t2:bool;
	let
		too_hot = (heating_on & t_in > 35);
		too_cold = (cooling_on & t_in < 5);
		both_on = (heating_on & cooling_on);
		(d,m,a,n) = shift(time);
		t1 = ((d & t_in < 20) or (m & t_in < 15) or (a & t_in < 10) or (n & t_in < 25));
		t2 = ((d & t_in > 25) or (m & t_in > 20) or (a & t_in > 15) or (n & t_in > 30));
	tel
	enforce (not (too_hot or too_cold or both_on) & (not t1 or heating_on) & (not t2 or cooling_on))
	with(c1,c2:bool)
let
	heating_on = inlined heating(c1);
	cooling_on = inlined cooling(c2);
tel